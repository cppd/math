/*
Copyright (C) 2017-2020 Topological Manifold

This program is free software: you can redistribute it and/or modify
it under the terms of the GNU General Public License as published by
the Free Software Foundation, either version 3 of the License, or
(at your option) any later version.

This program is distributed in the hope that it will be useful,
but WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
GNU General Public License for more details.

You should have received a copy of the GNU General Public License
along with this program.  If not, see <http://www.gnu.org/licenses/>.
*/

#version 450

// gl_GlobalInvocationID = gl_WorkGroupID * gl_WorkGroupSize + gl_LocalInvocationID

layout(local_size_x_id = 0) in;
layout(constant_id = 1) const bool INVERSE = true;
layout(constant_id = 2) const uint DATA_SIZE = 1;
layout(constant_id = 3) const uint N = 1;

const uint n_div_2_mask = N / 2 - 1;
const uint data_size = DATA_SIZE / 2;

layout(std140, binding = 0) uniform Data
{
        uint m_div_2;
        float two_pi_div_m;
};

layout(std430, binding = 1) buffer Buffer
{
        vec2 data[];
};

vec2 complex_mul(vec2 a, vec2 b)
{
        return vec2(a.x * b.x - a.y * b.y, a.x * b.y + a.y * b.x);
}

void main()
{
        uint thread_id = gl_GlobalInvocationID.x;
        if (thread_id >= data_size)
        {
                return;
        }

        // номер задачи внутри одного БПФ
        uint i = thread_id & n_div_2_mask;
        // начало ДПФ
        uint n_base = (thread_id - i) << 1;

        uint idx = i & (m_div_2 - 1);
        uint base = (i - idx) << 1;

        uint index_t = n_base + base + idx;
        uint index_u = index_t + m_div_2;

        vec2 v_t = data[index_t];
        vec2 v_u = data[index_u];

        if (!INVERSE)
        {
                vec2 twiddle_factor = vec2(cos(two_pi_div_m * idx), -sin(two_pi_div_m * idx));
                vec2 k = complex_mul(twiddle_factor, v_u);
                data[index_t] = v_t + k;
                data[index_u] = v_t - k;
        }
        else
        {
                vec2 twiddle_factor = vec2(cos(two_pi_div_m * idx), sin(two_pi_div_m * idx));
                vec2 k = complex_mul(twiddle_factor, v_u);
                data[index_t] = (v_t + k) * 0.5;
                data[index_u] = (v_t - k) * 0.5;
        }
}
