/*
Copyright (C) 2017-2024 Topological Manifold

This program is free software: you can redistribute it and/or modify
it under the terms of the GNU General Public License as published by
the Free Software Foundation, either version 3 of the License, or
(at your option) any later version.

This program is distributed in the hope that it will be useful,
but WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
GNU General Public License for more details.

You should have received a copy of the GNU General Public License
along with this program.  If not, see <http://www.gnu.org/licenses/>.
*/

#version 460

#extension GL_GOOGLE_include_directive : enable
#include "math.glsl"

// gl_GlobalInvocationID = gl_WorkGroupID * gl_WorkGroupSize + gl_LocalInvocationID

layout(constant_id = 0) const int FUNCTION_INDEX = 1;
layout(constant_id = 1) const bool INVERSE = true;
layout(constant_id = 2) const int N_1 = 1;
layout(constant_id = 3) const int N_2 = 1;
layout(constant_id = 4) const int M_1 = 1;
layout(constant_id = 5) const int M_2 = 1;
layout(local_size_x_id = 6, local_size_y_id = 7) in;

layout(std430, binding = 0) restrict buffer Data
{
        vec2 data[];
};

layout(std430, binding = 1) restrict buffer Buffer
{
        vec2 buf[];
};

// Formulas 13.4, 13.27, 13.28, 13.32.
vec2 mul_data(const vec2 x, const int l, const int n)
{
        // float theta = ((INVERSE ? PI : -PI) * l * l) / n;

        // Instead of l*l/n compute mod(l*l/n, 2) so that trigonometric
        // functions work with numbers no more than 2Ï€.
        // Avoid computing l*l using the float type. If l is less than
        // sqrt(2^31)=46340 then l*l is inside of the int range.

        const int dividend = l * l;
        const int quotient = dividend / n;
        const int remainder = dividend - quotient * n;

        // factor = (quotient mod 2) + (remainder / n).
        // float factor = (quotient & 1) + float(remainder) / n;
        // precise float theta = (INVERSE ? PI : -PI) * factor;
        // vec2 k = vec2(cos(theta), sin(theta));
        // return complex_mul(k, x);

        const float factor = float(remainder) / n;
        const precise float theta = (INVERSE ? PI : -PI) * factor;

        if ((quotient & 1) == 0)
        {
                const vec2 k = vec2(cos(theta), sin(theta));
                return complex_mul(k, x);
        }
        else
        {
                const vec2 k = vec2(-cos(theta), -sin(theta));
                return complex_mul(k, x);
        }
}

void rows_mul_to_buffer()
{
        const int i = int(gl_GlobalInvocationID.x);
        const int j = int(gl_GlobalInvocationID.y);

        if (i >= M_1 || j >= N_2)
        {
                return;
        }

        if (i >= N_1)
        {
                buf[j * M_1 + i] = vec2(0, 0);
        }
        else
        {
                const vec2 x = data[j * N_1 + i];
                buf[j * M_1 + i] = mul_data(x, i, N_1);
        }
}

void rows_mul_from_buffer()
{
        const int i = int(gl_GlobalInvocationID.x);
        const int j = int(gl_GlobalInvocationID.y);

        if (i >= N_1 || j >= N_2)
        {
                return;
        }

        const vec2 x = buf[j * M_1 + i];
        data[j * N_1 + i] = mul_data(x, i, N_1);
}

// with transposition
void columns_mul_to_buffer()
{
        const int i = int(gl_GlobalInvocationID.x);
        const int j = int(gl_GlobalInvocationID.y);

        if (j >= M_2 || i >= N_1)
        {
                return;
        }

        if (j >= N_2)
        {
                buf[i * M_2 + j] = vec2(0, 0);
        }
        else
        {
                const vec2 x = data[j * N_1 + i];
                buf[i * M_2 + j] = mul_data(x, j, N_2);
        }
}

// with transposition
void columns_mul_from_buffer()
{
        const int i = int(gl_GlobalInvocationID.x);
        const int j = int(gl_GlobalInvocationID.y);

        if (j >= N_2 || i >= N_1)
        {
                return;
        }

        const vec2 x = buf[i * M_2 + j];
        data[j * N_1 + i] = mul_data(x, j, N_2);
}

void main()
{
        if (FUNCTION_INDEX == 0)
        {
                rows_mul_to_buffer();
        }

        if (FUNCTION_INDEX == 1)
        {
                rows_mul_from_buffer();
        }

        if (FUNCTION_INDEX == 2)
        {
                columns_mul_to_buffer();
        }

        if (FUNCTION_INDEX == 3)
        {
                columns_mul_from_buffer();
        }
}
