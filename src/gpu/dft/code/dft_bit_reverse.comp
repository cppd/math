/*
Copyright (C) 2017-2024 Topological Manifold

This program is free software: you can redistribute it and/or modify
it under the terms of the GNU General Public License as published by
the Free Software Foundation, either version 3 of the License, or
(at your option) any later version.

This program is distributed in the hope that it will be useful,
but WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
GNU General Public License for more details.

You should have received a copy of the GNU General Public License
along with this program.  If not, see <http://www.gnu.org/licenses/>.
*/

#version 460

#extension GL_GOOGLE_include_directive : enable
#include "math.glsl"

// gl_GlobalInvocationID = gl_WorkGroupID * gl_WorkGroupSize + gl_LocalInvocationID

layout(local_size_x_id = 0) in;
layout(constant_id = 1) const uint DATA_SIZE = 1;
layout(constant_id = 2) const uint N_MASK = 1;
layout(constant_id = 3) const uint N_BITS = 1;

layout(std430, binding = 0) restrict buffer Buffer
{
        vec2 data[];
};

void main()
{
        const uint thread_id = gl_GlobalInvocationID.x;

        if (thread_id >= DATA_SIZE)
        {
                return;
        }

        const uint i = thread_id & N_MASK;
        const uint n_base = thread_id - i;
        const uint r = bit_reverse(i, N_BITS);

        if (i < r)
        {
                const uint i_index = n_base + i;
                const uint r_index = n_base + r;
                const vec2 t = data[i_index];
                data[i_index] = data[r_index];
                data[r_index] = t;
        }
}
