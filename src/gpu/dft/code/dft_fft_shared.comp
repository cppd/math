/*
Copyright (C) 2017-2026 Topological Manifold

This program is free software: you can redistribute it and/or modify
it under the terms of the GNU General Public License as published by
the Free Software Foundation, either version 3 of the License, or
(at your option) any later version.

This program is distributed in the hope that it will be useful,
but WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
GNU General Public License for more details.

You should have received a copy of the GNU General Public License
along with this program.  If not, see <http://www.gnu.org/licenses/>.
*/

#version 460

#extension GL_GOOGLE_include_directive : enable
#include "math.glsl"

// gl_GlobalInvocationID = gl_WorkGroupID * gl_WorkGroupSize + gl_LocalInvocationID

layout(constant_id = 0) const bool INVERSE = true;
layout(constant_id = 1) const uint DATA_SIZE = 1;
layout(constant_id = 2) const uint N = 1;
layout(constant_id = 3) const uint N_MASK = 1;
layout(constant_id = 4) const uint N_BITS = 1;
layout(constant_id = 5) const uint SHARED_SIZE = 1;
layout(constant_id = 6) const bool REVERSE_INPUT = true;
layout(local_size_x_id = 7) in;

layout(std430, binding = 0) restrict buffer Buffer
{
        vec2 global_data[];
};

const uint N_MAX = (N <= SHARED_SIZE) ? N : SHARED_SIZE;

shared vec2 shared_data[SHARED_SIZE];

void copy_global_to_shared()
{
        uint local = gl_LocalInvocationID.x;
        uint global = gl_WorkGroupID.x * SHARED_SIZE + gl_LocalInvocationID.x;

        while (local < SHARED_SIZE && global < DATA_SIZE)
        {
                if (REVERSE_INPUT)
                {
                        const uint i = global & N_MASK;
                        shared_data[local] = global_data[global - i + bit_reverse(i, N_BITS)];
                }
                else
                {
                        shared_data[local] = global_data[global];
                }

                local += gl_WorkGroupSize.x;
                global += gl_WorkGroupSize.x;
        }
}

void copy_shared_to_global()
{
        uint local = gl_LocalInvocationID.x;
        uint global = gl_WorkGroupID.x * SHARED_SIZE + gl_LocalInvocationID.x;

        while (local < SHARED_SIZE && global < DATA_SIZE)
        {
                global_data[global] = !INVERSE ? shared_data[local] : (shared_data[local] / N_MAX);

                local += gl_WorkGroupSize.x;
                global += gl_WorkGroupSize.x;
        }
}

void main()
{
        // One group works with SHARED_SIZE elements maximum.
        // There are 3 cases of DFT size (N):
        // * DFT size < SHARED_SIZE.
        //   Work with no more than DATA_SIZE elements.
        //   Example:
        //    10 DFT, each DFT has size 16, SHARED_SIZE = 128.
        //    Compute 8 DFT for the first group and 2 DFT for the second group (not 8).
        // * DFT size = SHARED_SIZE.
        // * DFT size > SHARED_SIZE.
        //   Compute up to SHARED_SIZE and without bit reverse.

        if (gl_WorkGroupSize.x > SHARED_SIZE / 2)
        {
                return;
        }

        if (DATA_SIZE <= gl_WorkGroupID.x * SHARED_SIZE)
        {
                return;
        }

        copy_global_to_shared();

        // One thread for 2 elements, so use >> 1
        const uint max_local = min(SHARED_SIZE, DATA_SIZE - gl_WorkGroupID.x * SHARED_SIZE) >> 1;

        // Starts with M = 2, so use PI
        float two_pi_div_m = INVERSE ? PI : -PI;

        for (uint m_div_2 = 1; m_div_2 < N_MAX; m_div_2 <<= 1, two_pi_div_m *= 0.5)
        {
                barrier();

                for (uint local = gl_LocalInvocationID.x; local < max_local; local += gl_WorkGroupSize.x)
                {
                        const uint idx = local & (m_div_2 - 1); // local % m_div_2
                        const uint base = (local - idx) << 1;

                        const uint index_t = base + idx;
                        const uint index_u = index_t + m_div_2;

                        const vec2 v_t = shared_data[index_t];
                        const vec2 v_u = shared_data[index_u];

                        const vec2 twiddle_factor = vec2(cos(two_pi_div_m * idx), sin(two_pi_div_m * idx));

                        const vec2 k = complex_mul(twiddle_factor, v_u);

                        shared_data[index_t] = v_t + k;
                        shared_data[index_u] = v_t - k;
                }
        }

        barrier();

        copy_shared_to_global();
}
