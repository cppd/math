/*
Copyright (C) 2017-2020 Topological Manifold

This program is free software: you can redistribute it and/or modify
it under the terms of the GNU General Public License as published by
the Free Software Foundation, either version 3 of the License, or
(at your option) any later version.

This program is distributed in the hope that it will be useful,
but WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
GNU General Public License for more details.

You should have received a copy of the GNU General Public License
along with this program.  If not, see <http://www.gnu.org/licenses/>.
*/

layout(local_size_x_id = 0, local_size_y_id = 1) in;
layout(constant_id = 2) const int RADIUS = 1;
layout(constant_id = 3) const int ITERATION_COUNT = 1;
layout(constant_id = 4) const float STOP_MOVE_SQUARE = 1;
layout(constant_id = 5) const float MIN_DETERMINANT = 1;
layout(binding = 4, r32f) readonly restrict uniform image2D image_dx;
layout(binding = 5, r32f) readonly restrict uniform image2D image_dy;
layout(binding = 6, r32f) readonly restrict uniform image2D image_i;
layout(binding = 7) uniform sampler2D texture_j;

layout(std430, binding = 0) readonly buffer TopPoints
{
        ivec2 top_points[];
};
layout(std430, binding = 1) writeonly buffer PointsFlow
{
        vec2 points_flow[];
};
layout(std430, binding = 2) readonly buffer PointsFlowGuess
{
        vec2 points_flow_guess[];
};

layout(std140, binding = 3) uniform Data
{
        int point_count_x;
        int point_count_y;
        bool use_all_points;
        bool use_guess;
        int guess_kx;
        int guess_ky;
        int guess_width;
};

vec2 pixel_to_texture(vec2 pixel)
{
        return vec2(pixel.x + 0.5, pixel.y + 0.5) / imageSize(image_dx);
}

vec2 get_dx_dy(ivec2 p)
{
        p = clamp(p, ivec2(0), imageSize(image_dx) - 1);
        return vec2(imageLoad(image_dx, p).r, imageLoad(image_dy, p).r);
}

float get_i(ivec2 p)
{
        p = clamp(p, ivec2(0), imageSize(image_dx) - 1);
        return imageLoad(image_i, p).r;
}
float get_j(vec2 p)
{
        p = clamp(p, vec2(0), imageSize(image_dx) - 1);
        return texture(texture_j, pixel_to_texture(p)).r;
}

bool compute_g_inv(ivec2 p, out mat2 g_inv)
{
        float sum_x2 = 0;
        float sum_y2 = 0;
        float sum_xy = 0;

        for (int i = -RADIUS; i <= RADIUS; ++i)
        {
                for (int j = -RADIUS; j <= RADIUS; ++j)
                {
                        ivec2 p = ivec2(p.x + i, p.y + j);

                        vec2 d = get_dx_dy(p);

                        sum_x2 += d.x * d.x;
                        sum_y2 += d.y * d.y;
                        sum_xy += d.x * d.y;
                }
        }

        // матрица задаётся по столбцам, но в данном случае нет разницы
        mat2 g = mat2(sum_x2, sum_xy, sum_xy, sum_y2);

        float det = determinant(g);
        if (abs(det) < MIN_DETERMINANT)
        {
                return false;
        }

        g_inv = inverse(g);

        return true;
}

vec2 get_guess(ivec2 p_screen)
{
        int guess_index = (p_screen.y / guess_ky) * guess_width + p_screen.x / guess_kx;
        vec2 flow = points_flow_guess[guess_index];
        return vec2(flow.x * guess_kx, flow.y * guess_ky);
}

void main(void)
{
        if (gl_GlobalInvocationID.x >= point_count_x || gl_GlobalInvocationID.y >= point_count_y)
        {
                return;
        }

        int point_index = int(gl_GlobalInvocationID.y * point_count_x + gl_GlobalInvocationID.x);

        const ivec2 p = use_all_points ? ivec2(gl_GlobalInvocationID.xy) : top_points[point_index];

        mat2 g_inv;

        if (!compute_g_inv(p, g_inv))
        {
                points_flow[point_index] = vec2(0);
                return;
        }

        vec2 v = vec2(0, 0);

        vec2 guess = use_guess ? get_guess(p) : vec2(0, 0);

        for (int i = 0; i < ITERATION_COUNT; ++i)
        {
                vec2 b = vec2(0, 0);

                for (int i = -RADIUS; i <= RADIUS; ++i)
                {
                        for (int j = -RADIUS; j <= RADIUS; ++j)
                        {
                                ivec2 pd = ivec2(p.x + i, p.y + j);

                                vec2 pd_j = vec2(pd) + guess + v;

                                b += (get_i(pd) - get_j(pd_j)) * get_dx_dy(pd);
                        }
                }

                vec2 n = g_inv * b;

                if (dot(n, n) < STOP_MOVE_SQUARE)
                {
                        break;
                }

                v += n;
        }

        vec2 res = v + guess;

        points_flow[point_index] = res;
}
