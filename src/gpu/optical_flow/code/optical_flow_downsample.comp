/*
Copyright (C) 2017-2026 Topological Manifold

This program is free software: you can redistribute it and/or modify
it under the terms of the GNU General Public License as published by
the Free Software Foundation, either version 3 of the License, or
(at your option) any later version.

This program is distributed in the hope that it will be useful,
but WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
GNU General Public License for more details.

You should have received a copy of the GNU General Public License
along with this program.  If not, see <http://www.gnu.org/licenses/>.
*/

#version 460

layout(local_size_x_id = 0, local_size_y_id = 1) in;

layout(binding = 0, r32f) readonly restrict uniform image2D image_big;
layout(binding = 1, r32f) writeonly restrict uniform image2D image_small;

// GaussianMatrix[2]
const int DELTA = 2;
const float FILTER_2D[5][5] = {
        { 0.002589, 0.0107788, 0.0241466, 0.0107788,  0.002589},
        {0.0107788, 0.0448755,   0.10053, 0.0448755, 0.0107788},
        {0.0241466,   0.10053,  0.225206,   0.10053, 0.0241466},
        {0.0107788, 0.0448755,   0.10053, 0.0448755, 0.0107788},
        { 0.002589, 0.0107788, 0.0241466, 0.0107788,  0.002589}
};
const float FILTER_1D[5] = {0.05088, 0.21184, 0.47456, 0.21184, 0.05088};

bool point_inside_big(const ivec2 p)
{
        return all(lessThanEqual(ivec2(0), p)) && all(lessThan(p, imageSize(image_big)));
}

float get_big(const ivec2 p)
{
        return imageLoad(image_big, p).r;
}

void set_small(const ivec2 p, const float v)
{
        imageStore(image_small, p, vec4(vec3(v), 1));
}

void main()
{
        const ivec2 p_small = ivec2(gl_GlobalInvocationID.xy);

        if (any(greaterThanEqual(p_small, imageSize(image_small))))
        {
                return;
        }

        const int kx = imageSize(image_big).x > imageSize(image_small).x ? 2 : 1;
        const int ky = imageSize(image_big).y > imageSize(image_small).y ? 2 : 1;

        const ivec2 p_big = ivec2(p_small.x * kx, p_small.y * ky);

        float sum_image = 0;
        float sum_filter = 0;

        if (kx > 1 && ky > 1)
        {
                for (int j = -DELTA; j <= DELTA; ++j)
                {
                        for (int i = -DELTA; i <= DELTA; ++i)
                        {
                                const ivec2 pd = ivec2(p_big.x + i, p_big.y + j);
                                if (point_inside_big(pd))
                                {
                                        const float m = FILTER_2D[j + DELTA][i + DELTA];
                                        sum_image += get_big(pd) * m;
                                        sum_filter += m;
                                }
                        }
                }
        }
        else if (kx > 1)
        {
                for (int i = -DELTA; i <= DELTA; ++i)
                {
                        const ivec2 pd = ivec2(p_big.x + i, p_big.y);
                        if (point_inside_big(pd))
                        {
                                const float m = FILTER_1D[i + DELTA];
                                sum_image += get_big(pd) * m;
                                sum_filter += m;
                        }
                }
        }
        else if (ky > 1)
        {
                for (int i = -DELTA; i <= DELTA; ++i)
                {
                        const ivec2 pd = ivec2(p_big.x, p_big.y + i);
                        if (point_inside_big(pd))
                        {
                                const float m = FILTER_1D[i + DELTA];
                                sum_image += get_big(pd) * m;
                                sum_filter += m;
                        }
                }
        }
        else
        {
                sum_image = get_big(p_big);
                sum_filter = 1;
        }

        set_small(p_small, sum_image / sum_filter);
}
