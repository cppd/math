/*
Copyright (C) 2017, 2018 Topological Manifold

This program is free software: you can redistribute it and/or modify
it under the terms of the GNU General Public License as published by
the Free Software Foundation, either version 3 of the License, or
(at your option) any later version.

This program is distributed in the hope that it will be useful,
but WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
GNU General Public License for more details.

You should have received a copy of the GNU General Public License
along with this program.  If not, see <http://www.gnu.org/licenses/>.
*/

// gl_GlobalInvocationID = gl_WorkGroupID * gl_WorkGroupSize + gl_LocalInvocationID

#if !defined(VULKAN)
// Вставляется в текст программы
// const uint GROUP_SIZE =
layout(local_size_x = GROUP_SIZE) in;
#else
layout(local_size_x_id = 0) in;
#endif

layout(std140, binding = 0) uniform Data
{
        uint max_threads;
        uint n_mask;
        uint n_bits;
};

layout(std430, binding = 1) buffer Buffer
{
        complex data[];
};

void main()
{
        uint thread_id = gl_GlobalInvocationID.x;

        if (thread_id >= max_threads)
        {
                return;
        }

        uint i = thread_id & n_mask;
        uint n_base = thread_id - i;
        uint r = bit_reverse(i, n_bits);

        if (i < r) // для исключения одинаковых обменов и уже сделанных обменов
        {
                uint i_index = n_base + i;
                uint r_index = n_base + r;
                complex t = data[i_index];
                data[i_index] = data[r_index];
                data[r_index] = t;
        }
}
